;; This software is Copyright (c) 2012 Chris Bagley
;; (techsnuffle<at>gmail<dot>com)
;; Chris Bagley grants you the rights to
;; distribute and use this software as governed
;; by the terms of the Lisp Lesser GNU Public License
;; (http://opensource.franz.com/preamble.html),
;; known as the LLGPL.

(in-package :varjo)

(defparameter *glsl-type-sizes*
  '((:bool . 1) (:int . 1) (:uint . 1) (:float . 1)
    (:bvec2 . 1) (:bvec3 . 1) (:bvec4 . 1)
    (:uvec2 . 1) (:uvec3 . 1) (:uvec4 . 1)
    (:ivec2 . 1) (:ivec3 . 1) (:ivec4 . 1)
    (:vec2 . 1) (:vec3 . 1) (:vec4 . 1)
    (:mat2 . 2) (:mat3 . 3) (:mat4 . 4)
    (:mat2x2 . 2) (:mat2x3 . 2) (:mat2x4 . 2)
    (:mat3x2 . 3) (:mat3x3 . 3) (:mat3x4 . 3)
    (:mat4x2 . 4) (:mat4x3 . 4) (:mat4x4 . 4)))

(defparameter *glsl-component-counts*
  '((:vec2 . 2) (:vec3 . 3) (:vec4 . 4)
    (:ivec2 . 2) (:ivec3 . 3) (:ivec4 . 4)
    (:uvec2 . 2) (:uvec3 . 3) (:uvec4 . 4)
    (:mat2 . 4) (:mat3 . 9) (:mat4 . 16)
    (:mat2x2 . 4) (:mat2x3 . 6) (:mat2x4 . 8)
    (:mat3x2 . 6) (:mat3x3 . 9) (:mat3x4 . 12)
    (:mat4x2 . 8) (:mat4x3 . 12) (:mat4x4 . 16)))

(defparameter *glsl-component-type*
  '((:bvec2 . :bool)   (:bvec3 . :bool)   (:bvec4 . :bool)
    (:uvec2 . :uint)   (:uvec3 . :uint)   (:uvec4 . :uint)
    (:ivec2 . :int)    (:ivec3 . :int)    (:ivec4 . :int)
    (:vec2 . :float)   (:vec3 . :float)   (:vec4 . :float)
    (:mat2 . :float)   (:mat3 . :float)   (:mat4 . :float)
    (:mat2x2 . :float) (:mat2x3 . :float) (:mat2x4 . :float)
    (:mat3x2 . :float) (:mat3x3 . :float) (:mat3x4 . :float)
    (:mat4x2 . :float) (:mat4x3 . :float) (:mat4x4 . :float)))

;; following 3 were constant
(defparameter *shader-types*
  '(:vertex :fragment :geometry))

(defparameter -default-version- :330)

(defparameter *implicit-type-casts*
  '(((:float nil nil) (:int nil nil) (:uint nil nil))
    ((:vec2 nil nil) (:ivec2 nil nil) (:uvec2 nil nil))
    ((:vec3 nil nil) (:ivec3 nil nil) (:uvec3 nil nil))
    ((:vec4 nil nil) (:ivec4 nil nil) (:uvec4 nil nil))
    ((:mat2 nil nil) (:mat2x2 nil nil))
    ((:mat3 nil nil) (:mat3x3 nil nil))
    ((:mat4 nil nil) (:mat4x4 nil nil))))

(defparameter *types* nil)

(defparameter *struct-definitions* nil)

(defparameter *built-in-types* 
  `((vgl-per-vertex-g nil nil "gl_per_vertex_g")
    (vgl-per-vertex-v nil nil "gl_per_vertex_v")
    (:none nil nil "none") (:void nil nil "void") 
    (:bool nil nil "bool") (:int nil nil "int") 
    (:uint nil nil "uint") (:float nil nil "float")
    (:double nil nil "double") (:bvec2 nil nil "bvec2") 
    (:bvec3 nil nil "bvec3") (:bvec4 nil nil "bvec4")
    (:uvec2 nil nil "uvec2") (:uvec3 nil nil "uvec3")
    (:uvec4 nil nil "uvec4") (:ivec2 nil nil "ivec2") 
    (:ivec3 nil nil "ivec3") (:ivec4 nil nil "ivec4")
    (:vec2 nil nil "vec2") (:vec3 nil nil "vec3")
    (:vec4 nil nil "vec4") (:mat2 nil nil "mat2") 
    (:mat3 nil nil "mat3") (:mat4 nil nil "mat4")
    (:mat2x2 nil nil "mat2x2") (:mat2x3 nil nil "mat2x3")
    (:mat2x4 nil nil "mat2x4") (:mat3x2 nil nil "mat3x2")
    (:mat3x3 nil nil "mat3x3") (:mat3x4 nil nil "mat3x4")
    (:mat4x2 nil nil "mat4x2") (:mat4x3 nil nil "mat4x3")
    (:mat4x4 nil nil "mat4x4") (:isampler-1D nil nil "isampler1D")
    (:isampler-1d-Array nil nil "isampler1DArray")
    (:isampler-2D nil nil "isampler2D")
    (:isampler-2d-Array nil nil "isampler2DArray")
    (:isampler-2d-MS nil nil "isampler2DMS")
    (:isampler-2d-MS-Array nil nil "isampler2DMSArray")
    (:isampler-2d-Rect nil nil "isampler2DRect")
    (:isampler-3d nil nil "isampler3d")
    (:isampler-Buffer nil nil "isamplerBuffer")
    (:isampler-Cube nil nil "isamplerCube")
    (:isampler-Cube-Array nil nil "isamplerCubeArray")
    (:sampler-1D nil nil "sampler1D")
    (:sampler-1d-Array nil nil "sampler1DArray")
    (:sampler-1d-Array-Shadow nil nil "sampler1DArrayShadow")
    (:sampler-1d-Shadow nil nil "sampler1DShadow")
    (:sampler-2D nil nil "sampler2D")
    (:sampler-2d-Array nil nil "sampler2DArray")
    (:sampler-2d-Array-Shadow nil nil "sampler2DArrayShadow")
    (:sampler-2d-MS nil nil "sampler2DMS")
    (:sampler-2d-MS-Array nil nil "sampler2DMSArray")
    (:sampler-2d-Rect nil nil "sampler2DRect")
    (:sampler-2d-Rect-Shadow nil nil "sampler2DRectShadow")
    (:sampler-2d-Shadow nil nil "sampler2DShadow")
    (:sampler-3d nil nil "sampler3d")
    (:sampler-Buffer nil nil "samplerBuffer")
    (:sampler-Cube nil nil "samplerCube")
    (:sampler-Cube-Array nil nil "samplerCubeArray")
    (:sampler-Cube-Array-Shadow nil nil "samplerCubeArrayShadow")
    (:sampler-Cube-Shadow nil nil "samplerCubeShadow")
    (:usampler-1D nil nil "usampler1D")
    (:usampler-1d-Array nil nil "usampler1DArray")
    (:usampler-2D nil nil "usampler2D")
    (:usampler-2d-Array nil nil "usampler2DArray")
    (:usampler-2d-MS nil nil "usampler2DMS")
    (:usampler-2d-MS-Array nil nil "usampler2DMSArray")
    (:usampler-2d-Rect nil nil "usampler2DRect") 
    (:usampler-3d nil nil "usampler3d")
    (:usampler-Buffer nil nil "usamplerBuffer")
    (:usampler-Cube nil nil "usamplerCube")
    (:usampler-Cube-Array nil nil "usamplerCubeArray")))

(defparameter *built-in-vars* 
  '((:core 
     (gl-max-clip-distances :int "gl_MaxClipDistances" t)
     (gl-max-clip-planes :int "gl_MaxClipPlanes" t)
     (gl-max-draw-Buffers :int "gl_MaxDrawBuffers" t)
     (gl-max-texture-units :int "gl_MaxTextureUnits" t)
     (gl-max-texture-coords :int "gl_MaxTextureCoords" t)
     (gl-max-geometry-texture-image-units :int 
      "gl_MaxGeometryTextureImageUnits" t)
     (gl-max-texture-image-units :int "gl_MaxTextureImageUnits" t)
     (gl-max-vertex-attribs :int "gl_MaxVertexAttribs" t)
     (gl-max-vertex-texture-image-units :int 
      "gl_MaxVertexTextureImageUnits" t)
     (gl-max-combined-texture-image-units :int 
      "gl_MaxCombinesTextureImageUnits" t)
     (gl-max-geometry-varying-components :int 
      "gl_MaxGeometryVaryingComponents" t)
     (gl-max-varying-floats :int "gl_MaxVaryingFloats" t)
     (gl-max-geometry-output-vertices :int 
      "gl_MaxGeometryOutputVertices" t)
     (gl-max-fragment-uniform-components :int 
      "gl_MaxFragmentUniformComponents" t)
     (gl-max-geometry-total-output-components :int 
      "gl_MaxGeometryTotalOutputComponents" t)
     (gl-max-geometry-uniform-components :int 
      "gl_MaxGeometryUniformComponents" t)
     (gl-max-vertex-uniform-components :int 
      "gl_MaxVertexUniformComponents" t))
    (:vertex 
     (gl-vertex-id :int "gl_VertexID" t)
     (gl-instance-id :int "gl_InstanceID" t)
     (gl-color :vec4 "gl_Color" t)
     (gl-secondary-color :vec4 "gl_SecondaryColor" t)
     (gl-normal :vec3 "gl_Normal" t)
     (gl-vertex :vec4 "gl_Vertex" t)
     (gl-multi-tex-coord-0 :vec4 "gl_MultiTexCoord0" t)
     (gl-multi-tex-coord-1 :vec4 "gl_MultiTexCoord1" t)
     (gl-multi-tex-coord-2 :vec4 "gl_MultiTexCoord2" t)
     (gl-multi-tex-coord-3 :vec4 "gl_MultiTexCoord3" t)
     (gl-multi-tex-coord-4 :vec4 "gl_MultiTexCoord4" t)
     (gl-multi-tex-coord-5 :vec4 "gl_MultiTexCoord5" t)
     (gl-multi-tex-coord-6 :vec4 "gl_MultiTexCoord6" t)
     (gl-multi-tex-coord-7 :vec4 "gl_MultiTexCoord7" t)
     (gl-fog-coord :float "gl_FogCoord" t)
     (gl-position :vec4 "gl_Position")
     (gl-point-size :float "gl_PointSize")
     (gl-clip-distance (:float t) "gl_ClipDistance")
     (gl-clip-vertex :vec4 "gl_ClipVertex")
     (gl-front-color :vec4 "gl_FrontColor")
     (gl-back-color :vec4 "gl_BackColor")
     (gl-front-secondary-color :vec4 "gl_FrontSecondaryColor")
     (gl-back-secondary-color :vec4 "gl_FrontSecondaryColor")
     (gl-tex-coord (:vec4 t) "gl_TexCoord")
     (gl-fog-frag-coord :float "gl_FogFragCoord"))
    (:fragment 
     (gl-frag-coord :vec4 "gl_FragCoord" t)
     (gl-front-facing :bool  "gl_FrontFacing" t)
     (gl-clip-distance (:float t) "gl_ClipDistance" t)
     (gl-point-coord :vec2  "gl_PointCoord" t)
     (gl-primitive-id :int "gl_PrimitiveID" t)
     (gl-frag-depth :float "gl_FragDepth" nil))
    (:geometry
     (gl-in (vgl-per-vertex-g t) "gl_in" t)
     (gl-primitive-id-in :int "gl_PrimitiveIDIn" t)
     (gl-position :vec4 "gl_Position")
     (gl-point-size :float "gl_PointSize")
     (gl-clip-distance (:float t) "gl_ClipDistance")
     (gl-primitive-id :int "gl_PrimitiveID")
     (gl-layer :int "gl_Layer"))))

(defparameter *glsl-variables* nil)
(defparameter *glsl-functions* nil)
(defparameter *glsl-special-functions* nil)
(defparameter *glsl-substitutions* nil)
(defparameter *shader-context* nil)

;;------------------------------------------------------------
;; Handy Functions
;;-----------------

;; [TODO] need assoc test... I dont thing so
(defun acons-many (data a-list)
  (if data (let* ((func (first data))
                  (name (first func))
                  (body (second func)))
             (acons name (cons body (rest (assoc name a-list)))
                    (acons-many (rest data) a-list)))
      a-list))

(defun kwd (&rest args) 
  (intern (format nil "狺狎珞щ妁黠蜾┅ㄤ彐躅骓ㄣ镱趄镬篝蜷铉蝈篝骘蝽狒狎珞ㄡ痧禊＇骘蝽狒啜铋泔铘蝻飙篝蜷铉梨矧磲舡狎珞┅ㄤ彐躅痱轭舡栳箬ㄨ狍璀翎忪濠祜镳骘忮轭翳栳箬脲镦栳箬翎忪轰痱轭ㄦ矧磲铋螈ㄧ弭栳箬栳箬翎忪濠┅栳箬翎忪濠ㄤ彐躅痱轭翩ㄣ镱趄镬篝蜷铉蝈篝骘蝽狒狎珲礤铘螬ㄡ痧禊＇骘蝽狒ㄡ痧孱扉篝泔铘蝻飙篝蜷铉骘蝽狒狎珲礤铘螬┅ㄤ彐躅珧秕箫躜沐瞟⒃栝翎脲骒狒扉篝犷屙轸扉篝镦扉篝蟋遽汨祜铉泔铘衢铋铉翳屐屙孱趔镦翳矧殓轭犰扉篝ㄩ弪镳瞟ㄥ蝌矧Ⅹ弪戾铉翳┅灬忮祗è蝈箫躜沐徙悌戾è蝈篝铘桡潋箫躜沐┅ㄩㄣ镱箴蝈篝蝈蝈篝ㄣ镱篚怏羼箫躜沐瞟徙悌铗弼弪箦ㄣ镱箫躜沐徙悌┅┅ㄩ箫躜沐蝈箫躜沐铋飑铋飑┅ㄤ彐躅簌礅é蝈篝狎珞⒃栝翎脲扉篝镦簌礅镬矧篝蜷铉螬犷秕麴豸镱簌礅镬涉翳轭瘐轶簌礅镬翳孱翳秕麴豸轶蝈珲灬簌礅镬涉翳轭瘐轶篝蜷铉蟋翳孱翳秕麴豸轶簌礅镬扉脲翳轶鲠祯弩ㄩ铘弪ㄦ矧磲铋狺狎珞┅┅ㄤ彐躅簌礅镬钺礤羼踽ㄡ猢麒孱ㄡ钿簌礅镬岍簌礅镬猢ㄥ聃犰簌礅镬钺礤岍簌礅镬钺礤猢┅ㄤ彐躅趄蹂铒铛祆┅ㄤ彐躅羼稹灬礅溽鲠飑ㄥ鲠┅ㄤ彐躅羼祓灬礅溽鲠飑ㄥ耢鲠┅ㄤ彐躅羼踽祓灬礅溽鲠飑ㄥ聃犰鲠┅ㄤ彐躅羼屐屙孱趔扉篝矧铛祆扉篝ㄥ鲥蝙ㄥ耩ㄣ狎扉篝┅扉篝┅ㄤ彐躅羼飙屐屙孱趔扉篝矧铛祆扉篝ㄥ鲥蝙ㄥ耢稹ㄣ狎扉篝┅扉篝┅ㄤ彐躅羼踽飙屐屙孱趔扉篝矧铛祆扉篝ㄥ鲥蝙ㄥ聃犰稹ㄣ狎扉篝┅扉篝┅ㄤ彐躅殇孱糸豉骈祠弪扉篝舡磲皓磲疸犷灬礅溽愆麒孱扉篝┅扉篝舡磲皓戾è泔躅癌ㄤ彐躅珈箪珏铙é镳糸镱犰钺礤鲠颟箦翩泔躅ǐ泔躅舂戾è筢驽钺礤筢驽珈钺礤钺礤┅ㄦ矧磲铋⑦徇幄筢驽钺礤泔躅舂┅ㄤ彐躅筢驽珈钺礤é蝈篝钺礤疳螋螬戾舄è篝蜷铉滹黝汜箦篝蜷铉ㄡ痧禊＇簌礅钺礤疳螋螬┅磲翥桢ㄣ飙痧泸搴犰飙磲翥桢③掎镰诎弓茛瞟┅ㄩ矧磲翥桢铒矧戾铉翳瞟博铒ㄥ聃犰㈢膦篚怏羼博┅┅ㄥ蝌矧⒅狎觑吾礤镦鲠蜷徕戾犷骢钽糸镱眭篝忮镱禊泔铘衢铪メ祓栳铛礤蜷汨狎徙翦蝮犷翳棂痂孱汨狎徙翦ō┊ピ桢犰箫磲铒篝狎鏖翳х歆幄瞟ㄣ飙痧泸搴蝈珏蝈痨徙瀛犰③茛⑦┅┅ㄤ彐躅簌礅镬钺礤痫箝糸镱簌礅镬扉篝戾è簌礅钺礤篝蜷铉躔汜箦簌礅镬┅痫箝糸镱殒＇灬礅溽麒孱簌礅镬ㄥ聃犰簌礅镬钺礤簌礅钺礤┅扉篝┅ㄤ彐磲泸狍箫泸ㄩ翦犰轶脲脲铋脲皓翦篝铋翦篝皓翦篝铒铋铒麴┅啜沅ㄡ篌镢轸屙犰轶括麒孱脲扉篝弘妁脲┅括麒孱翦篝扉篝呼弩翦篝┅括麒孱铒麴扉篝翦篝铒舂┅┅ㄤ彐躅扉篝蟓泔铘衢瞽漉痨殂狒弩é蝈篝扉篝螬戾è觑轭邃ㄡ痧禊＇狃疱钿扉篝螬┅铒ㄥ戾铉翳觑轭邃戾铉翳蝈盹鲥漉痨殂狒弩觑轭邃┅┅┅换换蔑溴渺狍换ㄤ彐沆狍泔溴īè豉疱箴邈洪铋翎蜱呼疱洪铋翩矧铋候遽溴泔溴豉疱瑚蜷翦箦翩泔溴豉疱┅ㄣ躜蝈铘扉铄洪铋翎蜱恒躜蝈铘扉铄洪铋翩矧铋候遽溴沲蝌孱舡扉铄瑚蜷翦箦翩沲蝌孱舡扉铄┅麸忪镢洪铋翎蜱呼锃忪镢洪铋翩矧铋候遽溴麸忪镢瑚蜷翦箦翩麸忪镢氅麸麸洪铋翎蜱呼锃麸洪铋翩矧铋候遽溴麸麸瑚蜷翦箦翩麸麸皓秕舡鲠蝮洪铋翎蜱猴豸鲠蝮洪铋翩矧铋候遽溴秕舡鲠蝮瑚蜷翦箦翩秕舡鲠蝮┅ㄩ铞狎獒铘洪铋翎蜱洪铞狎獒铘洪铋翩矧铋候遽溴轭鲠蜷犷瑚蜷翦箦翩轭鲠蜷犷舂蝈趱蝾洪铋翎蜱候弭躜铙洪铋翩矧铋候遽溴蝈趱蝾瑚蜷翦箦翩蝈趱蝾螬┅ㄤ彐礤翳镤轭轸獒扉瀛轭篝犷沐横骠弪è泔溴镡泔溴脲豉疱铋箦舡豉疱ㄣ躜蝈铘扉铄铋箦舡沲蝌孱舂ㄩ铒ㄡ钿箦舡沲蝌孱箦舡豉疱┅ㄥ蝌矧⒃疱犷沲蝌孱舡扉铄眭篝忮箴邈殒殄麒孱泸遽糸铉犷轭篝犷沐镦鲠蜿锖泔溴┅箦翩箪雉鲠祯泔溴镡豉疱箴邈ㄦ戾箬秕舡豉疱豉疱箪雉鲠祯泔溴镡с躜蝈铘扉铄沲蝌孱舡扉铄┅ㄤ彐珏铄蜷礤蜱瀛镡镡牦脲豉疱沲蝌孱舡扉铄麸忪镢麸麸秕舡鲠蝮轭鲠蜷犷蝈趱蝾螬ㄤ彐礤翳镤礤蜱瀛镡è镡牦扉篝脲豉疱沲蝌孱舡扉铄麸忪镢铋箦舡忪镢氅麸麸铋箦舡麸皓秕舡鲠蝮铋箦舡秕舡鲠蝮ㄩ铞狎獒铘铋飑蝈趱蝾铋箦舡蝈趱蝾螬磲脲轭篝犷沐с镤呼疱ㄩ豉疱豉疱ㄥ蝌矧Ⅳ疱轶磲钿狒矧┅恒躜蝈铘扉铄沲蝌孱舡扉铄呼锃忪镢ㄩ箦舡忪镢麸忪镢磲疸犷＇麸忪镢镡牦┅呼锃麸ㄩ箦舡麸麸麸磲疸犷＇麸麸镡牦┅猴豸鲠蝮ㄩ箦舡秕舡鲠蝮秕舡鲠蝮磲疸犷＇秕舡鲠蝮镡牦┅洪铞狎獒铘轭鲠蜷犷候弭躜铙ㄩ箦舡蝈趱蝾蝈趱蝾磲疸犷＇蝈趱蝾镡牦┅┅ㄤ彐礤翳镤礤蜱瀛镡è镡牦泔溴脲豉疱铋箦舡豉疱ㄣ躜蝈铘扉铄铋箦舡沲蝌孱舡扉铄麸忪镢铋箦舡忪镢氅麸麸铋箦舡麸皓秕舡鲠蝮铋箦舡秕舡鲠蝮ㄩ铞狎獒铘铋飑蝈趱蝾铋箦舡蝈趱蝾螬磲脲轭篝犷沐с镤呼疱ㄩ箦舡豉疱豉疱ㄣ镤瀛豉疱镡牦┅恒躜蝈铘扉铄ㄩ箦舡沲蝌孱舡扉铄沲蝌孱舡扉铄ㄣ躜蝈铘扉铄镡牦┅呼锃忪镢ㄩ箦舡忪镢麸忪镢麸忪镢镡牦┅呼锃麸ㄩ箦舡麸麸麸麸麸镡牦┅猴豸鲠蝮ㄩ箦舡秕舡鲠蝮秕舡鲠蝮秕舡鲠蝮镡牦┅洪铞狎獒铘轭鲠蜷犷候弭躜铙ㄩ箦舡蝈趱蝾蝈趱蝾蝈趱蝾镡牦┅┅换换翘犹赠疱换ㄤ彐躅骒弩璀秕舡豉疱鏖翳汨邈豉疱ㄩ铒扉篝豉疱┅ㄦ戾箬秕舡豉疱鏖翳汨邈扉篝豉疱┅ㄩㄩㄣ镱箴ㄦ轵篝豉疱┅ㄥ鲥蝙＇灬礅溽ㄡ篌镢豉疱螵┅ㄦ轵篝豉疱┅ㄡ篌镢ㄦ轵篝豉疱豉疱螵┅ㄦ戾箬秕舡豉疱豉疱ㄥ蝌矧⒅狎觑螫轶铒鲠扉豉疱轭翳轶泔铘屮幄豉疱箬徜弪泔铘屮舄┅┅ㄤ彐躅骒弩璀秕舡豉疱豉疱箴邈ㄩㄣ镱箴豉疱箴邈ㄩ戾铉翳豉疱箴邈穿ㄥ蝌矧⑸铞犰殇翘犹赠疱腻骈铋糸镱栳盹蝈翳犷泔眇镱孱趔豉疱箴邈扉篝豉疱痱轭汩痨豉疱箴邈豉疱狎蜥戾铉翳豉疱箴邈豉疱痨徙豉疱箴邈矧豉疱珈钺礤豉疱箴邈麒孱簌礅镬豉疱痱轭汩痨豉疱箴邈┅筢驽珈钺礤豉疱痱轭汩痨豉疱箴邈┅┅┅ㄦ戾箬秕舡豉疱扉篝豉疱箴邈┅┅ㄤ彐躅珈箪鲠扉洵豉疱ㄣ犷溟溽翦箴邈戾è豉疱ㄦ轵篝箴邈┅豉疱ㄦ轵篝汜钿殇狒濠戾铉翳箦泔钿箴邈┅戾铉翳箦泔钿汜钿殇狒濠┅铒铛祆ㄡ钿矧ㄥ豉疱舂ㄩ扉篝豉疱螬ㄦ轭豉疱豉疱螬ㄥ豉疱豉疱螬┅矧ㄥ戾铉翳戾铉翳螬ㄡ钿ㄥ戾铉翳舂戾铉翳悌麒孱ㄡ钿铛礅弪戾铉翳悌铛礅弪戾铉翳螬冀戾铉翳戾铉翳螬┅┅┅ㄤ彐躅箦舡痨徙瀛豉疱扉篝ㄦ轵篝豉疱箦泔钿豉疱舂ㄤ彐躅箦舡痨徙瀛铋豉疱扉篝ㄦ轵篝豉疱箦泔钿豉疱铋飑ㄤ彐躅珏舡痨徙翳轵┅ㄤ彐躅痨徙屦镡赍泗ㄧ弭痨徙ㄣ镤瀛豉疱镡赍泗┅ㄤ彐躅珈箪豉疱镡赍泗豉疱ㄧ祗飙鲠扉洵豉疱ㄣ镤瀛豉疱镡赍泗豉疱┅ㄤ彐躅豉疱羼踽ㄡ猢ㄥ聃犰篚怏羼博篚怏羼博┅换ㄤ彐躅豉疱痱轭汩痨豉疱ㄦ轵篝豉疱┅ㄤ彐躅豉疱狎蜥豉疱铒铛祆箦泔钿豉疱┅┅ㄤ彐躅豉疱狎蜥戾铉翳豉疱箦泔钿豉疱┅ㄤ彐躅豉疱痨徙豉疱翳轵豉疱┅ㄤ彐躅豉疱痨徙屦豉疱翳轵豉疱┅ㄤ彐躅豉疱珈钺礤豉疱ㄦ秕螋豉疱┅ㄤ彐躅豉疱怩殪舡轭豉疱铒铛祆ㄡ篌镢豉疱痱轭汩痨豉疱怩殪舡轭豉疱螵┅┅ㄤ彐躅怩殪舡轭鲠蝮ㄣ镱翦舂祜镳烘矧疳螋洪泔铘屮横痧孱ㄡ篌镢疳螋怩殪舡轭鲠蝮呼弩＇簌礅镬钺礤羼踽飑┅换ㄤ彐躅珈箪汜篝徕戾黹铒颦豉疱磲觑颦豉疱⒁弭躜铙麒弭桢翳豉疱黹铒颦豉疱汜忮汜篝躔麸豉疱磲觑颦豉疱矧豉疱羼踽磲觑颦豉疱黹铒颦豉疱铒铛祆ㄦ轭黹铒颦豉疱ㄡ篌镢磲觑颦豉疱轫痨殂轸豉疱汜篝螵呼弩＇豉疱羼踽飑呼弩＇豉疱羼踽飑┅┅ㄤ彐躅篚疱蜷矧豉疱é蝈篝豉疱螬㈡轭翳篚疱蜷矧豉疱豉疱狎溴骈铄轭矧溴矧篚疱蜷矧轸戾è豉疱篝蝈铉翳蝈盹鲥殒＇铛祆磲疸狎灬礅溽痫箝糸镱豉疱螵呼弩＇豉疱羼踽飑豉疱螬┅麒孱豉疱篝蝈铉翳ㄥ祠豉疱螵ㄡ痧禊＇磲豉疱篝蝈铉翳螬┅┅ㄤ彐躅豉疱蟓泔眇狒殁戾é蝈篝豉疱螬⑼犭篚蝈弼弪豉疱轶矧汜忮汜篝躔麸翳篚疱蜷矧豉疱戾è篚疱蜷矧ㄡ痧禊＇篚疱蜷矧豉疱豉疱螬┅ㄥ鲥蝙＇灬礅溽ㄧ祗飙汜篝徕戾篚疱蜷矧┅豉疱螬┅ㄤ彐躅豉疱徵珧彗狒瀛豉疱箴邈戾舄è骢祆豉疱ㄦ戾箬秕舡豉疱豉疱箴邈┅豉疱豉疱痱轭汩痨骢祆豉疱┅戾铉翳蝈篝ㄡ篌镢豉疱珈箪泔眇镱孱舡泔躅趔┅┅麒孱戾铉翳舂┅ㄤ彐躅豉疱泔眇镱孱舡泔躅豉疱箴邈戾舄è骢祆豉疱ㄦ戾箬秕舡豉疱豉疱箴邈┅豉疱豉疱痱轭汩痨骢祆豉疱┅戾铉翳ㄡ篌镢豉疱珈箪泔眇镱孱舡泔躅趔┅ㄩ戾铉翳戾铉翳ㄥ蝌矧⒃疱岌轶铒鲥泗矧矧磲趄轼泔眇镱犷豉疱骢祆豉疱┅┅ㄤ彐躅豉疱泔眇镱孱舡豉疱豉疱戾舄è骠疱ㄦ戾箬秕舡豉疱豉疱┅痿疱豉疱痱轭汩痨骠疱┅ㄩ豉疱狎蜥骠疱痿疱ㄩㄡ篌镢痿疱珈箪泔眇镱孱舡豉疱ㄡ篌镢痿疱珈箪泔眇镱孱舡豉疱ㄥ蝌矧⒃疱螫轶铒鲥泗矧矧磲趄轼泔眇镱犷豉疱痿疱┅┅ㄤ彐躅豉疱珈箪箝豉疱箴邈戾舄è骢祆豉疱ㄦ戾箬秕舡豉疱豉疱箴邈┅豉疱豉疱痱轭汩痨骢祆豉疱┅戾铉翳蝈篝ㄡ篌镢豉疱珈箪豉疱箝弩┅┅ㄩ戾铉翳戾铉翳ㄥ蝌矧⒃疱岌轶铒鲥泗矧矧磲趄轼泔眇镱犷豉疱豉疱┅┅ㄤ彐躅磲舡豉疱磲舡豉疱ㄥ聃犰⑼猎篚怏羼簌礅镬钺礤豉疱痱轭汩痨ㄦ戾箬秕舡豉疱磲舡豉疱┅畅┅ㄤ彐躅磲舣鲥悱戾铉翳磲舡豉疱戾è钺礤簌礅镬钺礤豉疱痱轭汩痨ㄦ戾箬秕舡豉疱磲舡豉疱┅┅疳蝮瀛轭翦珏篝蜷铉ㄥ祠钺礤ū戾铉翳钺礤┅┅┅ㄤ彐躅鲥悱豉疱磲舡豉疱戾è钺礤簌礅镬钺礤豉疱痱轭汩痨ㄦ戾箬秕舡豉疱磲舡豉疱┅┅ㄥ聃犰⒅琶篚怏羼钺礤磲ō戾铉翳钺礤穿ū戾铉翳钺礤┅┅┅ㄤ彐躅豉疱磲舡泔飙麸鲥磲舡豉疱膑ㄦ矧磲铋⒅琶幄ㄥ祠簌礅镬钺礤豉疱痱轭汩痨ㄦ戾箬秕舡豉疱磲舡豉疱┅畅┅ㄤ彐躅豉疱鲥悱泔蝈豉疱鲥悱豉疱戾舄è钺礤簌礅镬钺礤豉疱痱轭汩痨ㄦ戾箬秕舡豉疱鲥悱豉疱┅┅ㄩㄥ祠钺礤癌┅ㄣ镱è羼殇＼蜘烘祜狒è羼殇＼嫂洪铘è羼殇＼诈乎轭舂ㄥ蝌矧Ⅴ铍铒黝鲥泗矧豉疱┅┅ㄤ彐躅汨犷珏鲥悱戾铉翳鲥悱豉疱戾铉翳ㄩ豉疱狎蜥鲥悱豉疱ㄥ蝌矧⒅狎觑砒疱泗邃鲥泗矧顼鲥泗矧狎蜥戾舄è豉疱篝蜷铉豉疱痱轭汩痨鲥悱豉疱┅戾戾铉翳豉疱┅ㄢ狍篚怏羼豉疱ū戾瞟┅ㄦ戾箬秕舡豉疱膑忉箦戾铉翳┅┅换换翘犹契钽糸镱换ㄤ彐躅鲮犴怃é脲轭狎珞秕麴豸豉疱趄犷箧矧泔铘屮舡蝈篝蜷泗轱疳汶徵屐弩舂扉篝磲疸狎＇骒弩璀秕舡豉疱磲疸狎＇箦泔钿轭狎珞┅ㄦ戾箬秕舡豉疱秕麴豸豉疱趄犷箧矧磲疸狎＇灬礅溽ㄦ轭恒镯疳糸忪┅轭狎珞磲疸狎＇灬礅溽ㄦ轭喉狒汨┅轭狎珞泔铘屮舡蝈篝蜷泗轱瞟ㄤ彐躅骢钽轭箴邈ㄦ轵篝┅ㄤ彐躅骢钽秕舡箴邈箦泔钿┅ㄤ彐躅骢钽怙澌翳轵┅ㄤ彐躅骢钽泔眇狒殁戾狎珞ㄦ秕螋┅ㄤ彐躅骢钽狎珞磲翥ㄦ殒翳┅ㄤ彐躅骢钽蝈篝蜷泗轱箝翳┅ㄤ彐躅骢钽疳汶徵屐弩箦鲥铘┅ㄤ彐躅珈箪鲠扉洵骢钽糸镱狎珞ㄦ躅狎珞戾è轭箴邈ㄦ躅悱轭箴邈骢钽┅豉疱磲疸狎＇泔溴豉疱狎珞┅ㄡ钿ㄥ戾铉翳狎珞戾铉翳轭箴邈┅ㄥ鲥蝙＇灬礅溽ㄣ螬ㄩㄧ弭痨徙螬ㄧ弭痨徙悌舂豉疱轭箴邈ㄥ鲥蝙＇灬礅溽ㄣ螬ㄧ祗飙豉疱螬狎珞轭箴邈ㄡ痧禊＇豉疱蟓泔眇狒殁戾ㄩ溴铘轸骈祠弪豉疱ㄦ躅悱泔眇狒殁戾狎珞骢钽┅戾舄è骈祠弪邃豉疱ㄩ溴铘轸骈祠弪豉疱ㄦ躅悱狎珞磲翥骢钽┅ㄣ镯ㄦ轵篝骈祠弪邃豉疱螬┅铒翎铢＇铛祆磲疸狎＇灬礅溽豉疱羼踽泔眇┅骈祠弪邃豉疱螬┅┅ㄤ彐躅珈箪蝈箫祧瀛骢钽豉疱ㄦ躅狎珞换蝈趱蝾翳秕麴豸豉疱箴邈屮沐痿骘麒弪换翳箴邈疳螋轶铛礅弪轭麒殂汜箦麇换翎脲翳狒疳螋骝镯翳铛礅弪翳轭狎绠换物翦翳狒轭汜箦麒弪翳狎珞狎礤犷换麸忮泔眇狒殁戾翳狒礤犷麇铄邃麸翎脲换轸骝镯翳篚疱蜷矧轭狎豉疱戾舄è轭豉疱磲疸狎＇泔溴豉疱狎珞┅篚疱蜷矧ㄡ痧禊＇篚疱蜷矧豉疱ㄩ溴铘轸骈祠弪轭豉疱ㄦ躅悱泔眇狒殁戾狎珞骢钽┅┅磲溴豉疱祜镳烘矧轭ㄦ躅悱秕舡箴邈骢钽烘矧疳螋骝镯恒镬戾泗ㄩ铛礅弪椹铘疳螋ㄩ铘ㄦ躅悱泔眇狒殁戾狎珞骢钽┅篚疱蜷矧铘轭豉疱螬┅ㄩㄣ镱箴椹ㄦ轵篝椹椹┅ㄦ轭犰豉疱ㄦ戾箬秕舡豉疱磲溴豉疱┅换痱轭轭豉疱螬换痱轭ㄦ躅悱秕舡箴邈骢钽┅换痱轭ㄦ躅悱泔眇狒殁戾狎珞骢钽┅换痱轭磲溴豉疱换痱轭骈钺飙豉疱骈钺飙豉疱┅ㄤ彐躅镳弪箦珥孱舡扉篝扉篝簌礅镬ㄩ蝈篝扉篝扉篝簌礅镬ㄦ轵篝扉篝镳弪箦珥孱舡扉篝蝈篝扉篝簌礅镬┅ㄦ轵篝扉篝┅换换翘犹轴蜷徕戾换ㄤ彐躅鲠颦钺礤鲠颟ㄦ轵篝鲠颟ㄤ彐躅鲠颦豉疱鲠颟箦泔钿鲠颟ㄤ彐躅鲠颦珈钺礤鲠颟翳轵鲠颟ㄤ彐躅鲠颦蝈徜镱禊鲠颟ㄦ秕螋鲠颟ㄤ彐躅鲠颦蝈篝蜷泗轱鲠颟ㄦ殒翳鲠颟ㄤ彐躅泔眇殪瀛戾舡骘蝽戾舡骘蝽镳糸镱犰豉痖纟舂ㄧ孱簌憝鲠蝮舂换翎脲骘蝽犷蝈趱蝾扉篝镦赭翳轭珞换翳泔眇殪邃骘蝽犷翳鲠蜷徕戾骘蝽麒殂汜忮换狃疱钿邃麸珈箪鲠蜷徕戾螵灬忮祗è鲠颦钺礤ㄦ矧愆ㄩ扉篝ㄦ轵篝骘蝽┅ㄦ轵篝ㄦ轵篝骘蝽┅ㄦ轵篝骘蝽┅鲠颦豉疱ㄦ矧愆麒孱扉篝ㄦ轵篝骘蝽┅ㄦ戾箬秕舡豉疱箦泔钿ㄦ轵篝骘蝽┅┅鲠ㄦ矧愆箦泔钿骘蝽┅ㄣ镯痖戾骘蝽钺礤豉疱鲠祯濠ㄩ鲠祯ㄩ豉痖纟鲠蜿锃剧祗啜ヴ痖纟箦翩ē磲脲鲠钺礤豉疱鲠祯濠┅鲠蜿锃剧祗啜箦翩ē磲脲鲠钺礤豉疱鲠祯濠┅ㄩ豉痖纟鲠蜿锃剧祗啜ヴ痖纟ē磲脲鲠钺礤豉疱┅鲠蜿锃剧祗啜ロ犭瀛鲠钺礤豉疱┅┅戾舄è鲠飙镡牦祜镳烘矧骘蝽轭戾舡骘蝽恒镬戾泗鲠蜿锃剧祗鲠骘蝽┅┅鲠颦钺礤磲疸狎＇鲠颦钺礤戾舡骘蝽螬鲠颦珈钺礤ㄩ珏铙憝鲠蝮磲疸狎＇珈箪珏铙鲠颦钺礤螬磲疸狎＇筢驽珈钺礤鲠颦钺礤螬┅鲠颦豉疱祜镳烘矧骘蝽洪戾舡骘蝽烘矧镡洪鲠飙镡牦恒镬戾泗矧鲠颦豉疱骘蝽麒孱镡ㄣ镤瀛豉疱镡戛┅┅换匀徕秭汜忮铋麒孱翳鲠栳戽哌揶揶揶换镦翳戾骘蝽轶戾骠忪犷扉篝磲疸狎＇泔眇殪瀛骘蝽鲠颦珈钺礤鲠颦豉疱鲠飙镡牦磲疸狎＇扉篝鲠颦钺礤鲠颦豉疱鲠颦珈钺礤螬┅┅ㄤ彐躅鲠颦屮轶趔钺礤铒铛祆ㄡ篌镢钺礤珈箪鲠蜷徕戾螵呼弩＇簌礅镬钺礤羼踽飑┅换痱镡徕禊蝈漉钿犷ㄤ彐磲泸徜洵鲠蝮è鲠颦溴沆狎狒轱铙镳糸镱犰箬徜秣轭舂怙澌怙澌啜殒矧箬徜秣轭铒翎铢＇灬礅溽鲠颟鲠颦屮轶趔鲠颦钺礤鲠颟┅鲠颦溴沆狎狒轱铙┅戾è珈箪鲠蜷徕戾螵ㄡ痧孱鲠颦溴沆狎狒轱铙珈箪鲠蜷徕戾螵┅棱镤ㄥ蝌矧⒅狎獒忪犰蝈徜溴骈铄犷汜铑雉忮箬徜秣邃狺幄鲠颦溴沆狎狒轱铙珈箪鲠蜷徕戾螵┅换换翘犹郁蝓泗换ㄤ彐躅篝蝓泗轭轸骘蝽篝蝓泗戾舄è篝蝓泗钺礤ㄦ轵篝篝蝓泗┅箪雉蝈篝篝蝓泗┅ㄦ矧磲铋Ⅲ趄蹉狺狺};"
            struct-name (mapcar #'compile-struct-type slots))))

(defun compile-struct-type (slot)
  (let ((name (safe-gl-name (or (third slot) (first slot))))
        (type (flesh-out-type (second slot))))
    (let ((principle (varjo-type->glsl-type (first type)))
          (len (third type)))
      (if len
          (format nil "    a a[a];" 
                  principle name len)
          (format nil "    a a;" 
                  principle name)))))

(defun type-struct-p (type)
  (let ((ftype (flesh-out-type type)))
    (not (null (assoc (type-principle ftype) *struct-definitions*)))))

(defun struct-definition (type-name)
  (let ((descrip (assoc type-name *struct-definitions*)))
    (or (rest descrip) 
        (error "Varjo: Struct a does not exist" type-name))))

(defun get-struct-definitions (types)
  (if (not types) 
      (error "Varjo: get-struct-definitions called with no types")

      (let* ((found (loop for type in types 
                       :collect (assoc type
                                       *struct-definitions*)))
             (error-pos (position-if #'null found)))
        (if (not error-pos)
            found
            (error "Varjo: Struct a does not exist" 
                   (nth error-pos types))))))

(defun fake-struct-vars (var-name struct-name)
  (let ((slots (rest (first (get-struct-definitions 
                             (list struct-name))))))
    (loop for slot in slots
       :collect `(,(symb '-f- var-name '- (var-name slot))
		  ,(flesh-out-type (var-type slot))
		  ,(safe-gl-name '-f- var-name '- (var-name slot))))))

(defun make-fake-struct (struct-name)
  (let ((fake-type (symb '-f- struct-name))
        (slots (rest (first (get-struct-definitions 
                             (list struct-name))))))    
    (list
     (list struct-name fake-type)
     (loop :for slot :in slots 
        :collect
        (list (or (fifth slot) (symb struct-name '- (first slot)))
              (vlambda :in-args `((x (,fake-type)))
                       :output-type
                       (literal-number-output-type
                        (set-place-t 
                         (flesh-out-type 
                          (second slot))))
                       :transform (format nil "_f_~(~a_a~)" 
                                          (safe-gl-name (first slot)))))))))

(defun literal-number-output-type (type)
  (loop for i in type :collect (if (numberp i) (list i) i)))

(defun struct-funcs (struct)
  (%struct-funcs (first struct) nil nil 
		 (loop for slot in (rest struct)
		       collect (list (safe-gl-name (first slot))
				     (second slot)))))

(defun %struct-funcs (name slot-prefix context-restriction slots)
  (cons 
   (list (symb 'make- (or slot-prefix name))
         (vlambda :in-args (loop for slot in slots
                              :collect (subseq slot 0 2))
                  :output-type name
                  :transform (format nil "a(狺蕃)"
                                     name
                                     (loop for slot in slots
                                        collect "a"))
                  :context-restriction context-restriction))
   (loop :for slot :in slots 
      :collect
      (list (or (fifth slot)
                (symb (or slot-prefix name) '- (first slot)))
            (vlambda :in-args `((x (,name)))
                     :output-type 
                     (literal-number-output-type
                      (set-place-t (flesh-out-type 
                                    (second slot))))
                     :transform (format nil "~a.a" 
					(or (third slot) (first slot)))
                     :context-restriction context-restriction)))))

(defmacro vdefstruct (name &body slots)
  (let ((*types* (cons (list name nil) *built-in-types*))) 
    `(progn     
       (setf *glsl-functions* 
             (acons-many ',(%struct-funcs name nil nil slots)
                         *glsl-functions*))
       (setf *struct-definitions*
             (acons ',name ',slots
                    *struct-definitions*))
       ',name)))

(defmacro %vdefstruct (name (&key slot-prefix context-restriction)
                       &body slots)
  (let ((*types* (cons (list name nil) *built-in-types*))) 
    `(progn
       (setf *glsl-functions* 
             (acons-many ',(%struct-funcs name slot-prefix
                                          context-restriction 
                                          slots)
                         *glsl-functions*))
       (setf *built-in-types* 
             (acons ',name '(nil) *built-in-types*))
       ',name)))

;;------------------------------------------------------------

(defun varjo->glsl (varjo-code)
  (cond ((null varjo-code) nil)
        ((typep varjo-code 'code) varjo-code)
        ((atom varjo-code) 
         (if (assoc varjo-code *glsl-variables*
		    :test #'symbol-name-equal)
             (instance-var varjo-code)
             (error "Varjo: 's' is unidentified." varjo-code)))
        ((special-functionp (first varjo-code)) 
         (apply-special (first varjo-code) (rest varjo-code)))
        ((vfunctionp (first varjo-code))
         (compile-function (first varjo-code) (rest varjo-code)))
        (t (error "Function 's' is not available for A shaders in varjo." (first varjo-code) *shader-context*))))


(defun compile-function (func-name args)
  (let ((func-specs (func-specs func-name))
        (arg-objs (mapcar #'varjo->glsl args)))
    (loop :for f-spec :in func-specs 
       :if (glsl-valid-function-args f-spec arg-objs )
       :return (merge-obs arg-objs
                          :type (glsl-resolve-func-type f-spec arg-objs)
                          :current-line (apply #'format 
                                               (append 
                                                (list nil (func-body f-spec))
                                                (mapcar #'current-line
                                                        arg-objs))))
       :finally (error "There is no applicable method for the glsl function 's'%when called with argument types:%s " func-name (mapcar #'code-type arg-objs)))))

(defun macroexpand-and-substitute (varjo-code)
  (cond ((null varjo-code) nil)
        ((listp varjo-code) 
         (let ((sub (substitution (first varjo-code)))) 
           (if sub
               (mapcar #'macroexpand-and-substitute
                       (apply sub (rest varjo-code)))
               (mapcar #'macroexpand-and-substitute
                       varjo-code))))
        (t varjo-code)))

;; [TODO] How should we specify unsigned?
(defun replace-literals (varjo-code)
  (labels ((num-suffix (type)
	     (or (assocr (type-principle type) 
			 '((:float . "f") (:uint . "u")))
		 "")))
    (cond ((null varjo-code) nil)	 
	  ((eq t varjo-code) 
	   (make-instance 'code :current-line "true" 
				:type '(:bool nil)))
	  ((numberp varjo-code) 
	   (let ((num-type (get-number-type varjo-code)))
	     (make-instance 'code :current-line 
			    (format nil "aa" varjo-code
				    (num-suffix num-type))
				  :type num-type)))
	  ((listp varjo-code) (mapcar #'replace-literals
				      varjo-code))
	  (t varjo-code))))

(defun get-number-type (x)
  (cond ((floatp x) '(:float nil))
        ((integerp x) '(:int nil))
        (t (error "Varjo: Do not know the type of the number 's'"
                  x))))

(defun compile-var (name type &rest qualifiers)
  (%compile-var name type qualifiers))

(defun %compile-var (name type &optional qualifiers)
  (%qualify (varjo->glsl `(%in-typify (%make-var
                                       ,name 
                                       ,(flesh-out-type type))))
            qualifiers))

(defun %qualify (obj qualifiers)
  (merge-obs obj :current-line (format nil "()a" 
                                       qualifiers 
                                       (current-line obj))))

(defun qualify (obj &rest qualifiers)
  (%qualify obj qualifiers))

;;---------------------------------------------------------------

(defun glsl-defun (&key name in-args output-type
                     transform context-restriction)
  (let ((*types* *built-in-types*))
    (let* ((func-spec (vlambda :in-args in-args 
                               :output-type output-type
                               :transform transform
                               :context-restriction 
                               context-restriction)))
      (setf *glsl-functions*
            (acons name (cons func-spec
                              (assocr name *glsl-functions*
				      :test #'symbol-name-equal))
                   *glsl-functions*)))))

(defun get-vars-for-context (context)
  (loop for item in context
     :append (assocr item *built-in-vars*
		     :test #'symbol-name-equal)))


(defun func-valid-for-contextp (context func)
  (let ((restriction (func-restriction func)))
    (if restriction
        (when (every #'identity
                     (loop for item in restriction
                        :collect (find item context)))
          func)
        func)))

(defun func-specs (name)
  (let ((all-matching (assocr name *glsl-functions* 
			      :test #'symbol-name-equal)))
    (remove-if 
     #'null (loop for spec in all-matching
               :collect (func-valid-for-contextp 
                         *shader-context* spec)))))

(defun vfunctionp (name)
  (not (null (func-specs name))))

(defun special-functionp (symbol)
  (not (null (assoc symbol *glsl-special-functions*
		    :test #'symbol-name-equal))))

(defun apply-special (symbol arg-objs)
  (if (special-functionp symbol)
      (apply (assocr symbol *glsl-special-functions*
		     :test #'symbol-name-equal) arg-objs)
      (error "Varjo: 'a' is not a special function" symbol)))

(defun register-special-function (symbol function)
  (setf *glsl-special-functions* 
        (cons (cons symbol function) *glsl-special-functions*)))

(defmacro vdefspecial (name args &body body)
  `(register-special-function ',name #'(lambda ,args ,@body)))

(defun register-substitution (symbol function 
			      &optional (packageless nil))
  (setf *glsl-substitutions*
        (acons symbol (list function packageless)
	       *glsl-substitutions*)))

(defun substitutionp (symbol)
  (let ((sub (assoc symbol *glsl-substitutions*
		     :test #'symbol-name-equal)))
    (and (not (null sub))
	 (or (third sub) (eq symbol (first sub))))))

(defun substitution (symbol)
  (when (substitutionp symbol)
    (first (assocr symbol *glsl-substitutions*
		   :test #'symbol-name-equal))))

(defmacro vdefmacro (name lambda-list &body body)
  `(register-substitution
    ',name
    (lambda ,lambda-list
      ,@body)))

(defmacro %vdefmacro (name packageless lambda-list &body body)
  `(register-substitution
    ',name
    (lambda ,lambda-list
      ,@body)
    ,packageless))

(defun varjo-type->glsl-type (type-spec)
  (let* ((type (flesh-out-type type-spec))
         (type-name (or (type-gl-name type) (type-principle type)))
         (len (second type)))
    (if len
        (format nil "a[a]" type-name (if (numberp len) len ""))
        (format nil "a" type-name))))

(defun instance-var (symbol)
  (let ((var-spec (assoc symbol *glsl-variables*
			 :test #'symbol-name-equal)))
    (make-instance 'code
                   :type (let ((new-type (flesh-out-type (var-type var-spec))))
                           (if (var-read-only var-spec)
                               new-type
                               (set-place-t new-type)))
                   :current-line (format nil "a" 
                                         (or (var-gl-name var-spec)
                                             (var-name var-spec))))))


(defgeneric indent (input))

(defmethod indent ((input string))
  (mapcar #'(lambda (x) (format nil "    a" x))
          (split-sequence:split-sequence #\newline input)))

(defmethod indent ((input list))
  (mapcan #'indent input))

(defun indent-ob (code-obj)
  (merge-obs code-obj
             :to-block (indent (to-block code-obj))))

(defun make-none-ob ()
  (make-instance 'code :type :none
                 :current-line ""))

(defun end-line (ob)
  (if (find (type-principle (code-type ob)) '(:none :void))
      ob
      (merge-obs ob :current-line (format nil "a;" (current-line ob)))))


