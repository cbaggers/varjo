(in-package :varjo)

(defun cons-end (thing list)
  (concatenate 'list list (list thing)))

(defun listify (x) (if (listp x) x (list x)))
(defun delistify (x) (if (listp x)
                         (progn
                           (assert (= (length x) 1))
                           (first x))
                         x))

(defun lambda-list-get-names (l-list)
  (let ((keywords '(&allow-other-keys &environment &rest &aux &key &whole &body
                    &optional)))
    (loop :for i :in l-list
       :if (not (member i keywords))
       :collect (if (listp i) (first i) i))))

;; [TODO] fully implement positions-if to match position-if spec
;; [TODO] also add positions-if-not and positions: could be all be useful
(defun positions-if (predicate sequence)
  (let ((i -1))
    (labels ((f (accum x)
               (incf i)
               (if (funcall predicate x)
                   (cons i accum)
                   accum)))
      (reverse (reduce #'f sequence :initial-value nil)))))

(defun elt* (sequence &rest indicies)
  (labels ((_elt* (sequence indicies accum)
             (if indicies
                 (_elt* sequence
                        (rest indicies)
                        (cons (elt sequence (first indicies)) accum))
                 (reverse accum))))
    (_elt* sequence indicies nil)))


(defmacro pipe-> (args &body stages)
  "\(pipe-> \(1 2 3\) #'a #'b #'c #'d\)
   Calls first function with args provided and uses result as
   arguments for next function. Uses multiple-value-call so you
   can use (values) to specify complex lambda-args."
  (let ((stages (reverse stages)))
    (when stages
      (let ((stage (first stages)))
        (if (eq 'function (first stage))
            `(multiple-value-call ,stage
               ,(if (rest stages)
                    `(pipe-> ,args ,@(reverse (rest stages)))
                    (if (listp args)
                        `(values ,@args)
                        `(values-list ,args))))
            (destructuring-bind (check-func &rest steps) stage
              `(let ((rest (multiple-value-list
                            ,(if (rest stages)
                                 `(pipe-> ,args ,@(reverse (rest stages)))
                                 (if (listp args)
                                     `(values ,@args)
                                     `(values-list ,args))))))
                 (let ((args rest))
                   (let ((passes nil))
                     (loop :do (let ((results (multiple-value-list
                                               (pipe-> ,@(cons 'args steps)))))
                                 (setf args results)
                                 (push results passes))
                        :until (,check-func (first passes) (second passes))))
                   (values-list args)))))))))

(defmacro dbind (lambda-list expression &body body)
  `(destructuring-bind ,lambda-list ,expression ,@body))

(defmacro vbind (vars value-form &body body)
  ;; {TODO} handle declare forms properly. It is complicated
  ;;        as the declare has to be the first thing in the scope
  ;;        but the vars are now split across multiple binds
  (let* ((list? (mapcar #'listp vars))
         (mvb-vars (mapcar (lambda (v l?) (if l? (gensym) v)) vars list?))
         (d-vars (mapcar (lambda (v l?) (when l? v)) vars list?))
         (d-forms (mapcar (lambda (mvb d)
                            (when d `(dbind ,d ,mvb)))
                          mvb-vars d-vars))
         (d-forms (remove nil d-forms)))
    `(multiple-value-bind ,mvb-vars ,value-form
       (declare (ignorable ,@mvb-vars))
       ,@(reduce (lambda (accum x)
                   (list (append x accum)))
                 (cons body d-forms)))))

(defmacro vbind* (var-value-form-pairs &body body)
  (let ((pairs (group var-value-form-pairs 2)))
    (reduce (lambda (accum x) `(vbind ,@x ,accum))
            pairs :initial-value `(progn ,@body))))

(defmacro dbind* (lambda-list-expr-pairs &body body)
  (let ((pairs (group lambda-list-expr-pairs 2)))
    (reduce (lambda (accum x) `(dbind ,@x ,accum))
            pairs :initial-value `(progn ,@body))))

(defmacro vlist (value-form)
  `(multiple-value-list ,value-form))

;; [TODO] should dissapear as refactor goes on
(defun acons-many (data a-list)
  (if data (let* ((func (first data))
                  (name (first func))
                  (body (second func)))
             (acons name (cons body (rest (assoc name a-list)))
                    (acons-many (rest data) a-list)))
      a-list))

(defun kwd (&rest args)
  (intern (format nil "狺狎珞щ妁黠蜾┅换墼夏陷轶翳轶躞邃犷麒弪蹇ㄤ彐躅痱轭舡栳箬ㄨ狍璀翎忪濠祜镳骘忮轭翳栳箬脲镦栳箬翎忪轰痱轭ㄦ矧磲铋螈ㄧ弭栳箬栳箬翎忪濠┅栳箬翎忪濠ㄤ彐躅珧秕箫躜沐瞟⒃栝翎脲骒狒扉篝犷屙轸扉篝镦扉篝蟋遽汨祜铉泔铘衢铋铉翳屐屙孱趔镦翳矧殓轭犰扉篝ㄩ弪镳瞟ㄥ蝌矧Ⅹ弪戾铉翳┅灬忮祗è蝈箫躜沐徙悌戾è蝈篝铘桡潋箫躜沐┅ㄩㄣ镱箴蝈篝蝈蝈篝ㄣ镱篚怏羼箫躜沐瞟徙悌铗弼弪箦ㄣ镱箫躜沐徙悌┅┅ㄩ箫躜沐蝈箫躜沐铋飑铋飑┅ㄤ彐躅珧秕瓠怡脲箦聃孱沐脲翦篝＇羼踽飑⑶蝻躔怡脲犷磲轭翎轭矧溴颌戾è珧秕痼磲脲栳箬翎忪呼弩翦篝┅ㄩ癌灬忮祗è滹轸ㄥ戾è膂犰ㄦ躅汜祆脲濠┅ㄩㄧ弭栳箬膂犰珧秕痼瘐箬ㄧ弭栳箬膂犰珧秕痼┅箦翩ㄧ弭栳箬膂犰珧秕痼扉篝ㄩ钽殇┅┅┅磲铋＇滹轸箦聃孱沐┅磲疸狎＇蝈篝箫螋磲疸狎＇蝈鲥蝮ㄨ狍璀翎忪瀛鲠祯弩珧秕痼┅＇弘妁＇骈蝮舂┅ㄤ彐躅簌礅é蝈篝狎珞⒃栝翎脲扉篝镦簌礅镬矧篝蜷铉螬犷秕麴豸镱簌礅镬涉翳轭瘐轶簌礅镬翳孱翳秕麴豸轶蝈珲灬簌礅镬涉翳轭瘐轶篝蜷铉蟋翳孱翳秕麴豸轶簌礅镬扉脲翳轶鲠祯弩ㄩ铘弪ㄦ矧磲铋狺狎珞┅┅ㄤ彐躅瓠簌礅疳汶徵蝈篝狎珞⒃栝翎脲扉篝镦簌礅镬矧篝蜷铉螬犷秕麴豸镱簌礅镬涉翳轭瘐轶簌礅镬翳孱翳秕麴豸轶蝈珲灬簌礅镬涉翳轭瘐轶篝蜷铉蟋翳孱翳秕麴豸轶簌礅镬扉脲翳轶鲠祯弩ㄩ铘弪ㄦ矧磲铋狺狎珞疳汶徵濠┅ㄤ彐躅簌礅镬钺礤羼踽麒孱ㄡ钿簌礅镬簌礅镬┅ㄥ聃犰簌礅镬钺礤簌礅镬钺礤┅┅换墼夏陷麒轶翳轶弼弪铄邃邃ㄤ彐躅趄蹂铒铛祆┅换墼夏陷翳弩狎汜钿殇狒弩骘祜镳犰麽ㄤ彐躅羼稹灬礅溽鲠飑ㄥ鲠┅ㄤ彐躅羼祓灬礅溽鲠飑ㄥ耢鲠┅ㄤ彐躅羼踽祓灬礅溽鲠飑ㄥ聃犰鲠┅ㄤ彐躅羼屐屙孱趔扉篝矧铛祆扉篝ㄥ鲥蝙ㄥ耩ㄣ狎扉篝┅扉篝┅ㄤ彐躅羼飙屐屙孱趔扉篝矧铛祆扉篝ㄥ鲥蝙ㄥ耢稹ㄣ狎扉篝┅扉篝┅ㄤ彐躅羼踽飙屐屙孱趔扉篝矧铛祆扉篝ㄥ鲥蝙ㄥ聃犰稹ㄣ狎扉篝┅扉篝┅ㄤ彐躅簌礅镬钺礤痫箝糸镱簌礅镬扉篝戾è簌礅钺礤篝蜷铉躔汜箦簌礅镬┅痫箝糸镱殒＇灬礅溽麒孱簌礅镬ㄥ聃犰簌礅镬钺礤簌礅钺礤┅扉篝┅ㄤ彐躅狍箫泸ㄩ翦犰轶脲脲铋脲皓翦篝铋翦篝皓翦篝铒铋铒麴┅ㄣ潋ㄡ痧禊＇狍箫轸屙犰轶ㄡ痧孱麒孱脲扉篝弘妁脲┅麒孱翦篝扉篝呼弩翦篝┅麒孱铒麴扉篝翦篝铒舂┅┅ㄤ彐轭瀛泔眇殪弪磲泸狍箫泸ㄩ翦犰轶脲脲铋脲皓翦篝铋翦篝皓翦篝铒铋铒麴┅啜沅ㄡ篌镢轸屙犰轶括麒孱脲扉篝弘妁脲┅括麒孱翦篝扉篝呼弩翦篝┅括麒孱铒麴扉篝翦篝铒舂┅┅ㄤ彐躅扉篝泔铘衢铙漉痨殂狒弩扉篝脲脲＇殇孱糸豉翦篝＇羼┅祜镳烘矧洪扉篝轰麒孱ㄣ秕铘扉篝弘妁脲呼弩翦篝暴蝈趱蝾舂┅ㄤ彐躅骈钿漉痨殂狒弩扉篝戾è磲磲脲栳箬翎忪濠┅祜镳烘矧洪扉篝轰ㄩ钽ㄧ弭栳箬磲癌┅戾蝈篚祠磲痂狍灬礅溽雯麒孱暴瘐箬蝈篚祠┅磲皓蝈篚祠┅ㄤ彐躅灬篝扉篝ㄣ狎灬篝扉篝┅ㄤ彐躅灬礅溽扉篝箴扉翦眇灬翦灬憝扉篝灬忮祗è膑ㄩ铘弪ㄦ矧磲铋幄弘妁黠蜾┅簌礅镬钺礤ㄥ聃犰簌礅镬钺礤簌礅镬钺礤┅ㄣ镬戾泗矧灬憝扉篝镳糸镱犰沲蝌孱舡盹溟骈弪徙沲愆戾è轸屙ㄦ轵篝灬憝扉篝┅ㄣ镱è铛祆灬憝扉篝徙沲愆è犷簌礅镬轸屙ㄥ耢ㄥ祠簌礅镬钺礤轸屙癌＼Ιㄣ镬戾泗矧蝈篝灬憝扉篝膑轸屙徙沲愆ㄣ镬戾泗矧蝈篝灬憝扉篝沲蝌孱舡盹溟骈弪ㄡ泔铙沲蝌孱舡盹溟骈弪ㄣ镱轸屙ㄣ潋ㄡ篌镢沲蝌孱舡盹溟骈弪徙沲愆┅徙沲愆┅┅ㄣ戾犷犰轶ㄡ扉篝镳糸镱犰徙沲愆戾è轸屙ㄦ轵篝犰轶舂┅ㄣ镱è铛祆犰轶舂徙沲愆è狒镯轸屙ㄣ戾犷犰轶蝈篝犰轶舂徙沲愆è铒ㄡ篌镢ㄦ轵篝轸屙徙沲愆ㄣ戾犷犰轶蝈篝犰轶舂ㄣ镱轸屙徙沲愆┅ㄣ戾犷犰轶蝈篝犰轶舂徙沲愆┅┅蝈鲥蝮瀛蝈篚祠颟祜镳骘蝮舂轭泔祆邈ㄣ镱蝈鲥蝮蝮舂┅ㄦ轵篝轭翦眇灬翦矧铛祆ㄦ轵篝┅礤礅弪ㄦ轵篝翦眇灬翦呼弩＇簌礅镬钺礤┅┅戾è翦眇灬翦麒孱翦眇灬翦ㄣ镱铋磲疸狎＇膑翦眇灬翦┅┅箴扉ㄣ镬戾泗矧灬憝扉篝┅ㄩ矧铛祆翦眇灬翦ㄥ鲥蝙＇骈蝮舡轭翦眇灬翦箴扉舂蝈鲥蝮瀛蝈篚祠ㄣ戾犷犰轶箴扉舂戾舄èΝ簌眢蝈盹鲥殒铒灬礅溽麒孱簌礅镬ㄥㄥ祠簌礅镬钺礤癌＼Ι┅灬憝扉篝┅躅腩秣蝈盹鲥殒灬礅溽礤礅弪翦眇灬翦┅Ν簌眢┅ㄥ蝌矧ブ狎觑骑躅翳簌礅镬岙情鲥翳狒轸篝狎趔鏖翳Е轸祜镫扉脲灬礅溽扉篝脲黠蜾疹骘螋躅狒屐翳镱禊灬礅溽扉篝脲黠蜾翳狒狎篚痧矧翦轭翳轶泔铘屮狎搴螈ㄩ戾铉翳躅腩秣瞟暴Ⅲㄩ戾铉翳躅腩秣瞟暴ㄦ轵篝躅腩秣瞟躅腩秣瞟蝈盹鲥铋翦眇灬翦┅┅┅ㄤ彐躅箴扉舡狎珲礤铘ㄡ蜱镳糸镱犰翦眇灬翦Ж躅殒矧泔铘屮轭篝犷汩铉┅戾舄è箴扉灬礅溽扉篝箴扉翦眇灬翦狎珞┅ㄩ瞽狎珞ㄣ潋ㄡ篌镢铋箴扉舂┅ㄣ镱轭狎珞祜镳烘矧膑洪翦眇灬翦恒镬戾泗ㄣ潋ㄡ篌镢膑膑箴扉舂┅┅ㄤ彐躅狃痨趄邋ㄦ躅趄邋眭祠轲戾鲠祯瀛忾钿鲠汨犷珏洎ㄦ躅汜祆骢钽趄邋ㄩ汨犷珏鲠ㄩ扉篝趄邋磲疸狎灬礅溽ㄡ痧禊趄邋骢钽┅趄邋趄邋┅┅ㄤ彐躅蜥铉瞟祜镳骘忮祜泔祆邈椹ㄤ彐躅灬篝ㄣ狎灬篝┅ㄤ彐磲泸汜箦礤礅弪礤礅弪骘蝽怙澌汜箦螬戾è礤礅弪骘蝽扉篝殒礤礅弪骘蝽┅啜泔钿括祜镳烘矧ㄩ翦蝈篝洪汜箦恒镬戾泗ㄩ篝蜷铉羼踽轸屙э翳弪鏖箦啜莉弩舂啜礤礅弪轸屙理屙忮颦骘蝽莉弩舂┅┅ㄤ彐躅瞽镦翳轭泔躅舂祜镳烘矧衡屐秣泔躅恒镬戾泗翳轭绌ㄤ彐躅箴扉舡箦痱邃殂狒箦聃孱沐脲脲屦箴扉舂戾舄è篝狎暴祜镳烘矧孱痫箝糸镱殒痱邃殂狒箦聃孱沐后翎螋ū篝狎舂恒镬戾泗痱镧篚怏羼箦聃孱沐磲篝狎舂孱洎麒孱孱箦翩篝狎ㄩ脲屦箴扉孱ū孱洎┅┅瑚栝戾孱洎┅蝈盹鲥殒灬礅溽戾铉翳┅颟┅ㄤ彐磲泸狍箦螋轭ㄡ篌弪舡骘蝽弪蝻颦骘蝽蝈篝弪蝻颦狎珞啜戾狍箦螋骘蝽躅戾篌ㄡ钿括磲疸狎＇骈蝮狍箦螋骘蝽螬豉疱汜箦弪蝻颦骘蝽簌礅镬啜弪蝻К弪蝻颦骘蝽厘蝌矧狎珞┅篝蜷铉啜弪蝻Кㄦ矧磲铋狺狺" error-form
                                   (n-of "@[a]" (length error-args)))
                         ,@(loop :for e :in error-args
                              :for f :in assert-forms :collect
                              `(unless ,(first f) ,e))))
         (otherwise (error "The error-form used in the asserting macro must be a symbol or a string"))))))

(defmacro case= (form &body cases)
  (let ((g (gensym "val")))
    (labels ((wrap-case (c) `((= ,g ,(first c)) ,@(rest c))))
      (let* ((cases-but1 (mapcar #'wrap-case (butlast cases)))
             (last-case (car (last cases)))
             (last-case (if (eq (car last-case) 'otherwise)
                            `(t ,@(rest last-case))
                            (wrap-case last-case)))
             (cases (append cases-but1 (list last-case))))
        `(let ((,g ,form))
           (cond ,@cases))))))

(defun find-similarly-named-symbol (source-symb candidates-list)
  (when (symbolp source-symb)
    (let ((sn (symbol-name source-symb)))
      (remove-duplicates
       (remove-if-not
        (lambda (x)
          (let ((x (symbol-name x)))
            (or (string= sn x)
                (> (vas-string-metrics:jaro-winkler-distance sn x) 0.9))))
        candidates-list)))))
