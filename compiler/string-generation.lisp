(in-package :varjo)

(defun safe-glsl-name-string (name)
  (if (valid-user-defined-name name) 
      (string-downcase 
       (cl-ppcre:regex-replace-all "[-]" (symbol-name (symb name)) "_"))
      (error 'name-unsuitable :name name)))

(defun gen-reserved-var-string (name-symbol)
  (let* ((name-string (symbol-name name-symbol))
         (split-name (split-sequence #\- name-string :test #'equal)))
    (format nil "gl_狺祜镳烘矧疳螋洪箴扉舡钺礤洪铒ㄥ聃犰疳螋⑶挞┅恒镬戾泗ㄩ冀戾铉翳疳螋博篝蜷铉躔汜箦疳螋篝蜷铉汜痖翎扉疳螋┅┅┅ㄤ彐躅铛憝篚骀轼豉疱矧ㄡ篌镢豉疱爵疱箴邈豉疱Ж霏骒镝㈡霏蹰铘Ⅴ┅┅ㄤ彐躅珏瞽铛礅弪篝蜷铉铛礅弪豉疱ㄦ矧磲铋狺幄铛礅弪铛憝篚骀轼豉疱┅ㄤ彐躅珏瞽鲠蜷徕戾篝蜷铉鲠颦钺礤霏鲠祯濠ㄦ矧磲铋幄矧霏珈箪钺礤霏鲠祯濠篝蜷铉滹黝汜箦篝蜷铉鲠颦钺礤┅┅ㄤ彐躅珏瞽骢钽糸镱篝蜷铉ㄦ躅狎绛镡牦ㄡ痧禊＇骘蝽狒铋霏珈箪篝蜷铉骢钽磲疸狎＇沲蝌孱舡扉铄狎绛镡牦┅ㄤ彐躅珏瞽骢钽糸镱趄犷箧矧钺礤狎珞ㄦ矧磲铋屺狺蕃)" name (loop for i in args collect "a")))

(defun gen-function-signature (name args return-type)
  (format nil "a a((狺蕃));"
          (v-glsl-string return-type)
          name
          args))

(defun gen-function-body-string (name args type body-obj)
  (format nil "a a((狺蕃)) {%狺@[a%]}%"
          (v-glsl-string type)
          (string-downcase (string name)) 
          args
          (remove "" (to-block body-obj) :test #'equal) 
          (current-line (end-line body-obj))))

(defun gen-assignment-string (place val)
  (format nil "a = a" (current-line place) (current-line val)))

(defun gen-out-var-assignment-string (var-name val)
  (format nil "a = a" (safe-glsl-name-string var-name) (current-line val)))

(defun gen-if-string (test-obj then-obj else-obj)
  (if else-obj
      (format nil "a&if (a) {狺狺屐箦狺狺矧麸忪镢翦篝镡戛ㄣ躜蝈铘扉铄翦篝镡戛矧麸忪镢翳孱镡戛铋飑ㄣ躜蝈铘扉铄翳孱镡戛矧麸忪镢屐箦镡戛铋飑ㄣ躜蝈铘扉铄屐箦镡戛ㄦ矧磲铋狺殒岍狺狺矧麸忪镢翦篝镡戛ㄣ躜蝈铘扉铄翦篝镡戛矧麸忪镢翳孱镡戛铋飑ㄣ躜蝈铘扉铄翳孱镡戛┅ㄤ彐躅珏瞽麒殪瀛篝蜷铉翦篝镡怙澌镡戛ㄦ矧磲铋狺while (a) {%狺a;%}"
          (to-block test-obj)
          (current-line test-obj)
          (to-block body-obj)
          (current-line body-obj)))

(defun gen-swizzle-string (vec-obj components-string)
  (format nil "a.a" (current-line vec-obj) (string-downcase components-string)))

(defun gen-for-loop-string (var-string condition-obj update-obj body-obj)
  (format nil "for (a;a;a) {%狺a%}"
          var-string
          (current-line condition-obj)
          (current-line update-obj)
          (to-block body-obj)
          (current-line body-obj)))

(defun gen-switch-string (test-obj keys clause-body-objs
                          &optional (default-symb 'default))
  (let* ((default-clause nil)
         (format-clauses 
          (loop :for key :in keys
             :for obj :in clause-body-objs
             :append
             (if (eq key default-symb) 
                 (progn (setf default-clause (list "default" nil "jam")) nil)
                 (list key 
                       (or (to-block obj) nil) 
                       (current-line obj))) :into result
             :finally (return (append result default-clause))))) 
    (format nil "a%switch (a) {ャ狍岷狺撖a;%break;}}"
            (or (to-block test-obj) "") 
            (current-line test-obj)
                  format-clauses)))

(defun qualify (obj &rest qualifiers)
  (%qualify obj qualifiers))

(defun %qualify (obj qualifiers)
  (merge-obs obj :current-line (format nil "()a" 
                                       (string-downcase (string qualifiers)) 
                                       (current-line obj))))

(defun prefix-type-to-string (type line-string &optional qualifiers storage-qual)
  (let* ((line (cond ((typep type 'v-array) (format nil (v-glsl-string type)
                                                    line-string))
                     ((typep type 'v-type) (format nil "a a" 
                                                   (v-glsl-string type)
                                                   line-string))
                     (t (error "dont know how to add the type here")))))
    (if qualifiers
        (format nil "狺@[( a)] a" 
                (loop :for q :in qualifiers :collect (string-downcase (string q)))
                storage-qual
                line)
        (format nil "@[(a )]a" storage-qual line))))

(defun prefix-type-declaration (code-obj &optional qualifiers storage-qual)
  (prefix-type-to-string (code-type code-obj) (current-line code-obj) qualifiers
                         storage-qual))

(defun gen-out-var-string (name qualifiers value)
  (let ((name (if (stringp name) 
                  (string-downcase
                   (cl-ppcre:regex-replace-all "[-]" (symbol-name (symb name)) "_"))
                  (safe-glsl-name-string name))))
    (format nil "a;" (prefix-type-to-string (v-type value) name qualifiers 'out))))

(defun gen-in-var-string (name type qualifiers &optional layout)
  (let ((name (if (stringp name) 
                  (string-downcase
                   (cl-ppcre:regex-replace-all "[-]" (symbol-name (symb name)) "_"))
                  (safe-glsl-name-string name))))
    (format nil "@[layout(location = a) ]a;" layout
            (prefix-type-to-string type name qualifiers 'in))))

(defun gen-uniform-decl-string (name type)
  (format nil "uniform a;" (prefix-type-to-string type (safe-glsl-name-string name))))

;;[TODO] make this properly
(defun lisp-name->glsl-name (name)
  (string name))

(defun gen-shader-string (code-obj env)
  (format nil "#version a%狺}" (get-version-from-context env)
          (loop :for part :in 
             (list (used-types code-obj)
                   (mapcar #'fourth (v-in-args env))
                   (mapcar #'fourth (out-vars code-obj))
                   (mapcar #'third (v-uniforms env))
                   (signatures code-obj)
                   (to-top code-obj))
             :if part :collect part)))

;;----------------------------------------------------------------------

(defgeneric indent (input))

(defmethod indent ((input string))
  (mapcar #'(lambda (x) (format nil "    a" x))
          (split-sequence:split-sequence #\newline input)))

(defmethod indent ((input list))
  (mapcan #'indent input))

(defun indent-ob (code-obj)
  (merge-obs code-obj
             :to-block (indent (to-block code-obj))))
